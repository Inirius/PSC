#pragma kernel CSMain

Texture2D<float4> Source;
RWTexture2D<float4> Destination;

float4x4 _CameraToWorld;
float4x4 _CameraInverseProjection;

float3 _Light;
bool positionLight;

static const float maxDst = 80;
static const float epsilon = 0.001f;
static const float shadowBias = epsilon * 50;

struct Shape {
    
    float3 position;
    float3 size;
    float3 colour;
    int shapeType;
    int operation;
    float blendStrength;
    int numChildren;
    float3 rotation;
    float conductivity;
};

StructuredBuffer<Shape> shapes;
int numShapes;


struct Ray {
    float3 origin;
    float3 direction;
};

float3 Tourne(float3 rot,float3 coor) {
    return float3(coor.x*(cos(rot.y)*cos(rot.z)-sin(rot.y)*cos(rot.x)*sin(rot.z))-coor.y*(cos(rot.y)*sin(rot.z)+sin(rot.y)*cos(rot.z)*cos(rot.x))+coor.z*sin(rot.y)*sin(rot.x),
    coor.x*(sin(rot.y)*cos(rot.z)+cos(rot.y)*cos(rot.x)*sin(rot.z))+coor.y*(-sin(rot.y)*sin(rot.z)+cos(rot.y)*cos(rot.z)*cos(rot.x))-coor.z*cos(rot.y)*sin(rot.x),
    coor.x*sin(rot.x)*sin(rot.z)+coor.y*sin(rot.x)*cos(rot.z)+coor.z*cos(rot.x));
}

float3 TourneInv(float3 rot,float3 coor) {
    return float3(coor.x*(cos(rot.z)*cos(rot.y)-sin(rot.z)*cos(rot.x)*sin(rot.y))+coor.y*(cos(rot.z)*sin(rot.y)+sin(rot.z)*cos(rot.y)*cos(rot.x))+coor.z*sin(rot.z)*sin(rot.x),
    -coor.x*(sin(rot.z)*cos(rot.y)+cos(rot.z)*cos(rot.x)*sin(rot.y))+coor.y*(-sin(rot.z)*sin(rot.y)+cos(rot.z)*cos(rot.y)*cos(rot.x))+coor.z*cos(rot.z)*sin(rot.x),
    coor.x*sin(rot.x)*sin(rot.y)-coor.y*sin(rot.x)*cos(rot.y)+coor.z*cos(rot.x));
}

float SphereDistance(float3 eye, float3 centre, float radius) {
    return distance(eye, centre) - radius;
}

 ///........................
float CubeDistance(float3 eye, float3 centre, float3 size, float3 rot) {
    float3 o = abs(Tourne(rot,eye-centre)) -size;
    float ud = length(max(o,0));
    float n = max(max(min(o.x,0),min(o.y,0)), min(o.z,0));
    return ud+n;
}

// Following distance functions from http://iquilezles.org/www/articles/distfunctions/distfunctions.htm
float TorusDistance(float3 eye, float3 centre, float r1, float r2, float3 rot)
{   float3 coor = Tourne(rot,eye-centre);
    float2 q = float2(length(coor.xz)-r1,coor.y);
    return length(q)-r2;
}

float PrismDistance(float3 eye, float3 centre, float2 h, float3 rot) {
    float3 neye = Tourne(rot,eye-centre) ;
    float3 q = abs(Tourne(rot,eye-centre));
    return max(q.z-h.y,max(q.x*0.866025+ neye.y*0.5,- neye.y)-h.x*0.5);
}


float CylinderDistance(float3 eye, float3 centre, float2 h, float3 rot) {
    float3 neye = Tourne(rot,eye-centre);
    float2 d = abs(float2(length((neye).xz), neye.y)) - h;
    return length(max(d,0.0)) + max(min(d.x,0),min(d.y,0));
}

Ray CreateRay(float3 origin, float3 direction) {
    Ray ray;
    ray.origin = origin;
    ray.direction = direction;
    return ray;
}

Ray CreateCameraRay(float2 uv) {
    float3 origin = mul(_CameraToWorld, float4(0,0,0,1)).xyz;
    float3 direction = mul(_CameraInverseProjection, float4(uv,0,1)).xyz;
    direction = mul(_CameraToWorld, float4(direction,0)).xyz;
    direction = normalize(direction);
    return CreateRay(origin,direction);
}

// polynomial smooth min (k = 0.1);
// from https://www.iquilezles.org/www/articles/smin/smin.htm
float4 Blend( float a, float b, float3 colA, float3 colB, float k )
{
    float h = clamp( 0.5+0.5*(b-a)/k, 0.0, 1.0 );
    float blendDst = lerp( b, a, h ) - k*h*(1.0-h);
    float3 blendCol = lerp(colB,colA,h);
    return float4(blendCol, blendDst);
}

float4 Combine(float dstA, float dstB, float3 colourA, float3 colourB, int operation, float blendStrength) {
    float dst = dstA;
    float3 colour = colourA;

    if (operation == 0) {
        if (dstB < dstA) {
            dst = dstB;
            colour = colourB;
        }
    } 
    // Blend
    else if (operation == 1) {
        float4 blend = Blend(dstA,dstB,colourA,colourB, blendStrength);
        dst = blend.w;
        colour = blend.xyz;
    }
    // Cut
    else if (operation == 2) {
        // max(a,-b)
        if (-dstB > dst) {
            dst = -dstB;
            colour = colourB;
        }
    }
    // Mask
    else if (operation == 3) {
        // max(a,b)
        if (dstB > dst) {
            dst = dstB;
            colour = colourB;
        }
    }

    return float4(colour,dst);
}

float GetShapeDistance(Shape shape, float3 eye) {
   
    if (shape.shapeType == 0) {
        return SphereDistance(eye, shape.position, shape.size.x);
    }
    else if (shape.shapeType == 1) {
        return CubeDistance(eye, shape.position, shape.size, shape.rotation);
    }
    else if (shape.shapeType == 2) {
        return TorusDistance(eye, shape.position, shape.size.x, shape.size.y, shape.rotation);
    }
    else if (shape.shapeType == 3) {
        return PrismDistance(eye, shape.position, shape.size.xy, shape.rotation);
    }
    else if (shape.shapeType == 4) {
        return CylinderDistance(eye, shape.position, shape.size.xy, shape.rotation);
    }

    return maxDst;
}

float GetShapeDistance(Shape shape, float3 eye, float3 parent, float3 rot) {
   
    if (shape.shapeType == 0) {
        return SphereDistance(eye, parent + TourneInv(rot,shape.position - parent), shape.size.x);
    }
    else if (shape.shapeType == 1) {
        return CubeDistance(eye, parent + TourneInv(rot,shape.position - parent), shape.size, shape.rotation);
    }
    else if (shape.shapeType == 2) {
        return TorusDistance(eye, parent + TourneInv(rot,shape.position - parent), shape.size.x, shape.size.y, shape.rotation);
    }
    else if (shape.shapeType == 3) {
        return PrismDistance(eye, parent + TourneInv(rot,shape.position - parent), shape.size.xy, shape.rotation);
    }
    else if (shape.shapeType == 4) {
        return CylinderDistance(eye, parent + TourneInv(rot,shape.position - parent), shape.size.xy, shape.rotation);
    }

    return maxDst;
}


float4 SceneInfo(float3 eye) {
    float globalDst = maxDst;
    float3 globalColour = 1;
    
    for (int i = 0; i < numShapes; i ++) {
        Shape shape = shapes[i];
        int numChildren = shape.numChildren;

        float localDst = GetShapeDistance(shape,eye);
        float3 localColour = shape.colour;


        for (int j = 0; j < numChildren; j ++) {
            Shape childShape = shapes[i+j+1];
            float childDst = GetShapeDistance(childShape,eye,shape.position,shape.rotation);

            float4 combined = Combine(localDst, childDst, localColour, childShape.colour, childShape.operation, childShape.blendStrength);
            localColour = combined.xyz;
            localDst = combined.w;
        }
        i+=numChildren; // skip over children in outer loop
        
        float4 globalCombined = Combine(globalDst, localDst, globalColour, localColour, shape.operation, shape.blendStrength);
        globalColour = globalCombined.xyz;
        globalDst = globalCombined.w;        
    }

    return float4(globalColour, globalDst);
}

float3 EstimateNormal(float3 p) {
    float x = SceneInfo(float3(p.x+epsilon,p.y,p.z)).w - SceneInfo(float3(p.x-epsilon,p.y,p.z)).w;
    float y = SceneInfo(float3(p.x,p.y+epsilon,p.z)).w - SceneInfo(float3(p.x,p.y-epsilon,p.z)).w;
    float z = SceneInfo(float3(p.x,p.y,p.z+epsilon)).w - SceneInfo(float3(p.x,p.y,p.z-epsilon)).w;
    return normalize(float3(x,y,z));
}

float CalculateShadow(Ray ray, float dstToShadePoint) {
    float rayDst = 0;
    int marchSteps = 0;
    float shadowIntensity = .2;
    float brightness = 1;

    while (rayDst < dstToShadePoint) {
        marchSteps ++;
        float4 sceneInfo = SceneInfo(ray.origin);
        float dst = sceneInfo.w;
        
        if (dst <= epsilon) {
            return shadowIntensity;
        }

        brightness = min(brightness,dst*200);

        ray.origin += ray.direction * dst;
        rayDst += dst;
    }
    return shadowIntensity + (1-shadowIntensity) * brightness;
}

float _Radius = 10000;
float _Hot = 10;
float _Cold = 0;
float _Accuracy = 3;


float SDFCurrent(float3 position) {
    
    return SphereDistance(position, float3(10,0,0), 3);
}

float random(float2 x)
{
    return frac(sin(dot(x,float2(12.9898,78.233)))*43758.5453123);
}
float solvewos(float3 p0) {
    //if (SDFCurrent(p0)>0) return 1;
    float epsilon = 0.1;
    float sum = 0;
    int walkSamples = 64;
    float pi = 3.1415926;


    for (int walk = 0; walk < walkSamples; walk++) {
        float3 p = p0;
        float flag = 1;
        int iterations = 0;
        while (SDFCurrent(p) > epsilon && iterations < 100 && GetShapeDistance(shapes[0],p) < 0.2  ) {
            float internalRadius = abs(SDFCurrent(p));
            float theta = 2 * pi * random((walk+1)*p.xz);
            float phi = pi * random((walk*walk+1)*p.xy);
            p = float3(p.x + internalRadius * sin(theta) * cos(phi), p.y + internalRadius * sin(theta) * sin(phi), p.z + internalRadius * cos(theta));
            iterations++;
        }

        float tempe;

        if (SDFCurrent(p) <= epsilon) tempe = 1;
        else tempe = 0;

        sum += tempe / walkSamples;
    }
    return sum;
}

[numthreads(8,8,1)]
void CSMain (uint3 id : SV_DispatchThreadID)
{
    uint width,height;
    Destination.GetDimensions(width, height);

    Destination[id.xy] = Source[id.xy];

    float2 uv = id.xy / float2(width,height) * 2 - 1;
    float rayDst = 0;

    Ray ray = CreateCameraRay(uv);
    int marchSteps = 0;

    while (rayDst < maxDst) {
        marchSteps ++;
        float4 sceneInfo = SceneInfo(ray.origin);
        float dst = sceneInfo.w;
        
        if (dst <= epsilon) {
            float3 pointOnSurface = ray.origin + ray.direction * dst;
            float3 normal = EstimateNormal(pointOnSurface - ray.direction * epsilon);
            float3 lightDir = (positionLight)?normalize(_Light-ray.origin):-_Light;
            float lighting = saturate(saturate(dot(normal,lightDir)));
            float3 col = sceneInfo.xyz;
            
            // Shadow
            float3 offsetPos = pointOnSurface + normal * shadowBias;
            float3 dirToLight = (positionLight)?normalize(_Light- offsetPos):-_Light;

            ray.origin = offsetPos;
            ray.direction = dirToLight;

            float dstToLight = (positionLight)?distance(offsetPos,_Light):maxDst;
            float shadow = CalculateShadow(ray, dstToLight);

            float wos = 0;

            if(GetShapeDistance(shapes[0],pointOnSurface)<0.01){
                wos = solvewos(pointOnSurface);
            }

            Destination[id.xy] = float4(col * lighting * shadow,1) + shapes[0].conductivity  * wos * float4(1,0,0,0);

            break;
        }

        ray.origin += ray.direction * dst;
        rayDst += dst;
    }
}
